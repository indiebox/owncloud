developer=http://owncloud.org/
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=8.2.2
pkgrel=3
pkgdesc="Your cloud, your data, your way!"
arch=('any')
license=("GPL")
source=("http://download.owncloud.org/community/owncloud-${pkgver}.tar.bz2")
# source=("http://download.owncloud.org/community/testing/owncloud-${pkgver}.tar.bz2")
options=('!strip')
sha512sums=('3830e8311931da3cbb596e927afe638dee6a5d9fd47d623faf4dabbde96fe39ced1bf7006c1b9e39ff52e72e2b849fe9d1012c3b1a86ca9f8c873d820b944b78')

prepare() {
    patch ${startdir}/src/owncloud/lib/private/security/trusteddomainhelper.php ${startdir}/allow-wildcard-trusted.patch
}

package() {
# Manifest
    mkdir -p ${pkgdir}/var/lib/ubos/manifests
    install -m0644 ${startdir}/ubos-manifest.json ${pkgdir}/var/lib/ubos/manifests/${pkgname}.json

# Icons
    mkdir -p ${pkgdir}/srv/http/_appicons/$pkgname
    install -m644 ${startdir}/appicons/{72x72,144x144}.png ${pkgdir}/srv/http/_appicons/$pkgname/

# Data
    mkdir -p ${pkgdir}/var/lib/owncloud
    
# Code
    mkdir -p ${pkgdir}/usr/share/${pkgname}/bin
    install -m755 ${startdir}/bin/{create-ocdata,fix-permissions,fix-restore,generate-autoconfig,upgrade,web-install}.pl ${pkgdir}/usr/share/${pkgname}/bin/

    mkdir -p ${pkgdir}/usr/share/${pkgname}/tmpl
    install -m644 ${startdir}/tmpl/htaccess.tmpl ${pkgdir}/usr/share/${pkgname}/tmpl/
    touch ${pkgdir}/usr/share/${pkgname}/tmpl/ocdata

    cp -dr --no-preserve=ownership ${startdir}/src/owncloud ${pkgdir}/usr/share/${pkgname}/
}
